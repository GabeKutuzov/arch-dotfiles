           %TITLE "UDEV - GENERATE UNIFORM AND NORMAL RANDOM NUMBERS"
;**********************************************************************
;                                                                     *
;     ROCKS CRYSTALLOGRAPHIC COMPUTING SYSTEM - C IMPLEMENTATION      *
;                                                                     *
;     This program contains udev, a routine to generate a single      *
;     uniformly distributed random number, and ndev, a routine to     *
;     generate a single normally distributed random number.  This     *
;     is the TURBOC version.                                          *
;                                                                     *
;**********************************************************************
;                                                                     *
;     Algorithm:  Uniformly distributed pseudo-random integers        *
;        are generated by ir = seed*(7**5) MODULO (2**31-1).          *
;        The first eight bits of each are used to select from a       *
;        look-up table an interval of width 0.125 in the range        *
;        0.0-3.0 with appropriate frequency.  The remaining bits      *
;        specify the location within the chosen intervala and         *
;        the sign.  The resulting value is multiplied by 'sigma'      *
;        and finally 'mean' is added.  The result is a piecewise      *
;        rectangular approximation to the true Gaussian distribu-     *
;        tion, good enough for most purposes.  See Knuth, Vol. 2.     *
;                                                                     *
;**********************************************************************
;
;        V1A, 03/27/89, G. N. REEKE
;
           .287
           IFNDEF  MDL
MDL        EQU     LARGE
           ENDIF
           .MODEL  MDL,C
           .CODE

;---------------------------------------------------------------------*
;                                                                     *
;                        long udev(long *seed)                        *
;                                                                     *
;---------------------------------------------------------------------*

           PUBLIC  udev
udev       PROC
           ARG     usptr:PTR DWORD
           LOCAL   ustat:WORD =USIZE   
           les     bx,usptr            ;Seed pointer to es:bx
           fild    DWORD PTR cs:bigmod ;Pick up divisor
           fild    DWORD PTR es:bx     ;Pick up seed
           fimul   DWORD PTR cs:ranmul           
udevrep:   fprem                       ;Calculate remainder exactly
           fstsw   ustat               ;Check for completion
           test    ustat,0400h
           jnz     udevrep             ;Repeat until done
           fistp   DWORD PTR es:bx     ;Save new seed
           fstp    st(0)               ;Pop the divisor off
           fwait
           mov     ax,WORD PTR es:bx   ;Pick up remainder for return
           mov     dx,WORD PTR es:bx+2
           ret                         ;Return
udev       ENDP

;---------------------------------------------------------------------*
;                                                                     *
;    long ndev(long *seed, long cmn /* S24 */, long csg /* S28 */)    *
;                                                                     *
;---------------------------------------------------------------------*

           PUBLIC  ndev
ndev       PROC
           ARG     nsptr:PTR DWORD,cmn:DWORD,csg:DWORD
           LOCAL   ndqw:QWORD =NSIZE   
           les     bx,nsptr            ;Seed pointer to es:bx
           fild    DWORD PTR cs:bigmod ;Pick up divisor
           fild    DWORD PTR es:bx     ;Pick up seed
           fimul   DWORD PTR cs:ranmul           
ndevrep:   fprem                       ;Calculate remainder exactly
           fstsw   WORD PTR ndqw       ;Check for completion
           test    WORD PTR ndqw,0400h
           jnz     ndevrep             ;Repeat until done
           fist    DWORD PTR es:bx     ;Save new seed
           ffree   st(1)               ;Clear out the divisor
           fild    WORD PTR cs:scale10 ;Pick up scale
           fxch
           fscale                      ;Give 22 fraction bits
           ffree   st(1)               ;Clear out the scale factor
           fistp   ndqw                ;Store for fiddling
           fwait
           xor     ax,ax               ;Clean up ax
           mov     bx,WORD PTR ndqw+4  ;Pick up integer part
           shr     bx,1                ;Sign to carry flag
           mov     al,cs:ntab[bx]      ;Pick up rectangle number
           mov     WORD PTR ndqw+4,ax  ;Construct new value
           fild    ndqw                ;Back to long format
           fwait
           jnc     SHORT ndevpos       ;Adjust the sign
           fchs
ndevpos:   fild    WORD PTR cs:scalm39 ;Pick up scale
           fxch
           fscale                      ;Give 28 fraction bits
           ffree   st(1)               ;Clear out the scale factor
           fimul   csg                 ;Times sigma (S28)
           fiadd   cmn                 ;Plus mean (S24)
           fistp   ndqw                ;Result to memory
           fwait
           mov     ax,WORD PTR ndqw    ;Result to ax,dx pair
           mov     dx,WORD PTR ndqw+2
           ret                     ;Return
ndev       ENDP

;----------------------------------------------------------------------*
;                                                                      *
;               Constants for random number generation                 *
;                                                                      *
;----------------------------------------------------------------------*

           ALIGN   2
ranmul     dd      16807           ;Multiplier
bigmod     dd      07fffffffh      ;Divisor
scale10    dw      10              ;Scale factor 10
scalm39    dw      -39             ;Scale factor -39
;
;          Table of probabilities for normal distribution generator
;
ntab       LABEL   BYTE
           DB      25 DUP (0)
           DB      25 DUP (1)
           DB      24 DUP (2)
           DB      23 DUP (3)
           DB      22 DUP (4)
           DB      20 DUP (5)
           DB      18 DUP (6)
           DB      16 DUP (7)
           DB      15 DUP (8)
           DB      13 DUP (9)
           DB      11 DUP (10)
           DB      9 DUP (11)
           DB      8 DUP (12)
           DB      6 DUP (13)
           DB      5 DUP (14)
           DB      4 DUP (15)
           DB      3 DUP (16)
           DB      2 DUP (17)
           DB      2 DUP (18)
           DB      19,20,21,22,23
           END
